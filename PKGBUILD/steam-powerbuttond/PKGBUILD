# Maintainer: Fewtarius
# Thanks to upsteam maintainer: Derek J. Clark <derekjohn.clark@gmail.com>
pkgname=steam-powerbuttond
pkgver=20240829
pkgrel=2
pkgdesc="Simple powerbutton daemon for steam with gamescope-session"
arch=('any')
url="https://github.com/ShadowBlip/steam-powerbuttond"
license=('GPL')
groups=()
depends=('python' 'python-evdev')
optdepends=()
makedepends=('git')
source=("${pkgname}::git+${url}.git"
	0001-Don-t-install-to-usr-local.patch
	0002-fix-finding-user.patch)
sha256sums=('SKIP'
	    '2ada504d2cbc0f3bbcc53226be9fcf2ff7abae4e41d46f3568cd9c4ad40e18c4' # 0001-Don-t-install-to-usr-local.patch
	    'af4913f01377ba9cad03588decee29e7e170e0288df1597446ac0403ed96387b' # 0002-fix-finding-user.patch
           )

package() {
  cd "${srcdir}/${pkgname}"

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done

  mkdir -p ${pkgdir}/usr/lib/systemd/system
  install -m644 steam-powerbuttond.service ${pkgdir}/usr/lib/systemd/system

  mkdir -p ${pkgdir}/usr/bin
  install -m755 steam-powerbuttond ${pkgdir}/usr/bin
}

