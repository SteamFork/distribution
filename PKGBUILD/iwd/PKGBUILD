# Maintainer: Fewtarius
# Upstream:
#   https://gitlab.archlinux.org/archlinux/packaging/packages/iwd
#   Maintainer : Christian Rebischke <chris.rebischke@archlinux.org>
#   Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
#   Contributor: AndyRTR <andyrtr at archlinux.org>

pkgname=iwd
pkgver=3.0
pkgrel=1
pkgdesc='Internet Wireless Daemon'
arch=('x86_64')
url='https://git.kernel.org/cgit/network/wireless/iwd.git/'
license=('LGPL-2.1-or-later')
depends=('glibc' 'readline' 'libreadline.so' 'ell' 'gcc-libs')
makedepends=('python-docutils' 'dbus' 'systemd' 'ell')
optdepends=('qrencode: for displaying QR code after DPP is started')
source=(https://www.kernel.org/pub/linux/network/wireless/iwd-${pkgver}.tar.xz
        0001-use-network-group-for-unprivileged-access.diff
        0002-fix-scan-segfault.patch)
# https://mirrors.edge.kernel.org/pub/linux/network/wireless/sha256sums.asc
sha256sums=('bd167ab368b6ba302b6c948a4f41f02d233a12e20d5094b1c0393325309f8a60'
            'd5fb4fb864b7a0632117aa2039df535ab5c1d024ae618a1f09e34dfab8ee0cc7'
            '4471026fb8816132e7f89327d2f0d72b63b6c8c64ffb1c449205dfe52bfec6ac')
# https://lore.kernel.org/iwd/20240122104541.74f1a697@workstation64.local/T/#u
options=('!lto')

prepare() {
  cd ${pkgname}-${pkgver}
  # replace Debian "netdev" group with existing "network" group
  # for unprivileged access to iwd - avoid also log spam
  # https://gitlab.archlinux.org/archlinux/packaging/packages/iwd/-/issues/2 + #3
  patch -Np1 -i ../0001-use-network-group-for-unprivileged-access.diff

  # fix segfault
  # https://gitlab.archlinux.org/archlinux/packaging/packages/iwd/-/issues/5
  # patch -Np1 -i ../0002-fix-scan-segfault.patch

  # https://lore.kernel.org/iwd/20240122105312.66fb4dbf@workstation64.local/T/#u
  # disable one expected test failure - requires a kernel module we cannot load
  # from inside the chroot
  sed -i "s:unit/test-wsc::" Makefile.am
  sed -i "s:unit/test-eap-sim::" Makefile.am
  autoreconf -vfi
}

build() {
  cd ${pkgname}-${pkgver}
	dbus-run-session ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--libexecdir=/usr/lib/iwd \
	--enable-external-ell \
	--enable-wired \
	--enable-ofono \
	--enable-hwsim \
	--disable-tools
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make install DESTDIR="${pkgdir}"
  install -Dm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
}
