# Maintainer: Fewtarius
# Upstream maintainer: William Edwards <shadowapex@gmail.com>
pkgname=inputplumber
_pkgbase=inputplumber
pkgver=0.44.1
pkgrel=1
pkgdesc="Open source input router and remapper daemon for Linux"
arch=('x86_64')
options=(!lto)
url="https://github.com/ShadowBlip/InputPlumber"
license=('GPL')
depends=('dbus' 'libevdev' 'libiio')
makedepends=('rust' 'make' 'cmake' 'clang' 'clang-libs' 'git' 'gcc')
provides=('inputplumber')
conflicts=('inputplumber-bin')
source=("${_pkgbase}::git+https://github.com/ShadowBlip/${_pkgbase}.git#tag=v$pkgver"
	001-no-rootfs.patch
	002-enable-gyro-air_zero.patch
)

sha256sums=('SKIP'
	'55d15e63e2229e239b51823ba1a92522cba4a04e424c8515c210c102b4b6a88c'  # 001-no-rootfs.patch
	'050ed7b0cf854c86cecda952864b64a0563b2a193d8c231f37cd052398148501'  # 002-enable-gyro-air_zero.patch
)

build() {
	cd "$srcdir/${_pkgbase}"
	for patch in ../*patch
	do
		patch -p1 <${patch}
	done
	make build
}

package() {
	cd "$srcdir/${_pkgbase}"
	make install PREFIX=${pkgdir}/usr NO_RELOAD=true
	### Suspend service
	mkdir -p ${pkgdir}/etc/systemd/system/sleep.target.wants
	cd ${pkgdir}/etc/systemd/system/sleep.target.wants
	ln -sf /usr/lib/systemd/system/inputplumber-suspend.service .
	cd -
}
