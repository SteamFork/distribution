# Maintainer: Fewtarius
# Upstream maintainer: William Edwards <shadowapex@gmail.com>
pkgname=inputplumber
_pkgbase=inputplumber
pkgver=0.39.1
pkgrel=2
pkgdesc="Open source input router and remapper daemon for Linux"
arch=('x86_64')
options=(!lto)
url="https://github.com/ShadowBlip/InputPlumber"
license=('GPL')
depends=('dbus' 'libevdev' 'libiio')
makedepends=('rust' 'make' 'cmake' 'clang' 'clang-libs' 'git' 'gcc')
provides=('inputplumber')
conflicts=('inputplumber-bin')
source=("${_pkgbase}::git+https://github.com/ShadowBlip/${_pkgbase}.git#tag=v$pkgver"
	001-no-rootfs.patch
	003-Update-mount_matrix.patch
)

sha256sums=('SKIP'
	'55d15e63e2229e239b51823ba1a92522cba4a04e424c8515c210c102b4b6a88c'  # 001-no-rootfs.patch
	'4bf2d523156045bd2a25a4fcf7f1ab9937d40ac3a2bb981a55e9bc73e38f92bf'  # 003-Update-mount_matrix.patch
)

build() {
	cd "$srcdir/${_pkgbase}"
	for patch in ../*patch
	do
		patch -p1 <${patch}
	done
	make build
}

package() {
	cd "$srcdir/${_pkgbase}"
	make install PREFIX=${pkgdir}/usr NO_RELOAD=true
}
