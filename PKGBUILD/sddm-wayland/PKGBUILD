# Maintainer: Fewtarius
# Upstream:
#   Maintainer: John Schoenick <johns@valvesoftware.com>
#   Contributor: Piotr Gorski <lucjan.lucjanov@gmail.com>
#   Contributor: Jerome Leclanche <jerome@leclan.ch>

_pkgbase="sddm"
_srccommit="d19813cb03c7d71b896aead28e5285bc131500a9"
pkgname="$_pkgbase-wayland"
pkgver="v0.19.0.valve4.${_srccommit#v}"
pkgrel=1.1
pkgdesc="The Simple Desktop Display Manager - development branch"
arch=("x86_64")
url="https://github.com/sddm/sddm/"
license=("GPL")
depends=('qt5-declarative' 'xorg-xauth' 'xorg-server')
makedepends=('extra-cmake-modules' 'python-docutils' 'qt5-tools' 'git' 'python' )
provides=("$_pkgbase" "$_pkgbase-git" 'display-manager')
conflicts=("$_pkgbase" "$_pkgbase-git")
backup=('usr/share/sddm/scripts/Xsetup'
        'usr/share/sddm/scripts/Xstop'
        'etc/pam.d/sddm'
        'etc/pam.d/sddm-autologin'
        'etc/pam.d/sddm-greeter')
source=("sddm::git+https://github.com/sddm/sddm.git#commit=$_srccommit"
        sddm.sysusers sddm.tmpfiles)
sha256sums=('SKIP'
            '9fce66f325d170c61caed57816f4bc72e9591df083e89da114a3bb16b0a0e60f'
            'db625f2a3649d6d203e1e1b187a054d5c6263cadf7edd824774d8ace52219677')

build() {
  cmake -B build -S $_pkgbase \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
        -DDBUS_CONFIG_DIR=/usr/share/dbus-1/system.d \
        -DDBUS_CONFIG_FILENAME=sddm_org.freedesktop.DisplayManager.conf \
        -DBUILD_MAN_PAGES=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 sddm.sysusers "$pkgdir"/usr/lib/sysusers.d/sddm.conf
  install -Dm644 sddm.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/sddm.conf

  install -d "$pkgdir"/usr/lib/sddm/sddm.conf.d
  "$pkgdir"/usr/bin/sddm --example-config > "$pkgdir"/usr/lib/sddm/sddm.conf.d/default.conf
  # Don't set PATH in sddm.conf
  sed -r 's|DefaultPath=.*|DefaultPath=/usr/local/sbin:/usr/local/bin:/usr/bin|g' -i "$pkgdir"/usr/lib/sddm/sddm.conf.d/default.conf
  # Unset InputMethod https://github.com/sddm/sddm/issues/952
  sed -e "/^InputMethod/s/qtvirtualkeyboard//" -i "$pkgdir"/usr/lib/sddm/sddm.conf.d/default.conf
}
