# Maintainer: Fewtarius

pkgname=steamfork-device-support
pkgver=2025.02.19
pkgrel=2
pkgdesc='Hardware support provider for PC handhelds.'
arch=('any')
url='http://www.steamfork.org'
license=('LGPLv2+')
depends=('steamfork-customizations'
	 'ectool')

installit() {
  TYPE=$(file --no-sandbox "${1}")
  if [[ "${TYPE}" =~ script ]]
  then
    PERM="755"
  elif [[ "${TYPE}" =~ link ]]
  then
    FILE_PATH="${pkgdir}/$(dirname "${1}")"
    if [ ! -d "${FILE_PATH}" ]
    then
      echo "Mkdir ${FILE_PATH}"
      mkdir -p "${FILE_PATH}"
    fi
    TARGET=$(readlink "$1")
    echo "Changedir ${FILE_PATH}"
    cd "${FILE_PATH}"
    echo "Linking ${TARGET} ."
    ln -sf ${TARGET} $(basename ${1})
    cd -
    return
  else
    PERM="644"
  fi
  echo "Install ${PERM}: ${1}"
  install -Dm ${PERM} "${1}" "${pkgdir}/${1}"
}

package() {
  cd "${srcdir}" || exit 2
  find -type f -o -type l | while read FILE
  do
    echo "Install ${FILE}"
    installit "${FILE}"
  done

  ### System services
  mkdir -p ${pkgdir}/usr/lib/systemd/system/multi-user.target.wants
  cd ${pkgdir}/usr/lib/systemd/system/multi-user.target.wants
  for service in steamfork-boot-quirks.service  steamfork-fancontrol.service steamfork-battery-watchdog.service steamfork-bluetooth-watchdog.service
  do
    ln -sf ../${service} .
  done
  cd -

  ### User services
  mkdir -p ${pkgdir}/usr/lib/systemd/user/default.target.wants
  cd ${pkgdir}/usr/lib/systemd/user/default.target.wants
  for service in steamfork-audio-watchdog.service
  do
    ln -sf ../${service} .
  done
  cd -

  mkdir -p ${pkgdir}/usr/lib/systemd/system/steamos-offload.target.wants
  cd ${pkgdir}/usr/lib/systemd/system/steamos-offload.target.wants
  ln -s ../etc-lib-hwsupport-devicequirks.mount .
  cd -

  mkdir -p ${pkgdir}/etc/lib/hwsupport/devicequirks
}
